rootProject.name = 'mybatis-plus'

def isSubproject = { File file ->
    file.isDirectory() &&
            !file.name.contains('.git') &&
            !file.name.contains('.gradle') &&
            !file.name.contains('.idea') &&
            !file.name.contains('build') &&
            !file.name.contains('gradle')
}

String[] subprojects = rootDir.listFiles().findAll(isSubproject).collect { it.name }

include subprojects


dependencyResolutionManagement {
    versionCatalogs {
        libs {
            version('groovy', '3.0.5')
            version('checkstyle', '8.37')
            library('groovy-core', 'org.codehaus.groovy:groovy:3.0.5')
            library('groovy-json', 'org.codehaus.groovy:groovy-json:3.0.5')
            library('groovy-nio', 'org.codehaus.groovy:groovy-nio:3.0.5')
            library('commons-lang3', 'org.apache.commons', 'commons-lang3').version {
                strictly '[3.8, 4.0['
                prefer '3.9'
            }
            bundle('groovy', ['groovy-core', 'groovy-json', 'groovy-nio'])
        }
    }
}
